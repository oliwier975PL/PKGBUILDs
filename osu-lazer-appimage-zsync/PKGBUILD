# I have no explanation for what I just did here...
pkgname=osu-lazer-appimage-zsync
_pkgname=osu-lazer
pkgver=2020.1204.0
pkgrel=1
pkgdesc="A free-to-win rhythm game. Rhythm is just a click away!"
arch=('x86_64')
url="https://github.com/ppy/osu"
license=('MIT')
provides=('osu-lazer' 'osu')
conflicts=('osu-lazer' 'osu')
depends=('ffmpeg' 'zlib')
makedepends=('zsync-curl' 'git')
options=(!strip)
source=("${_pkgname}".png::https://github.com/ppy/osu/raw/master/assets/lazer.png
        "LICENCE"::https://github.com/ppy/osu/raw/master/LICENCE
        "${_pkgname}".desktop)
sha256sums=('36f73cfe0a84cd65a8bb54fcde5a01c419b134bee4a88cc92eb4f33236343a10'
            'e60cde328f1484d4b1505e3e7d446b1052635fddad7f59393a2d458132181e6c'
            'c9a8fb0f0711338b98aa12e6e8c30889ca475e7f181578637416b0083c58c663')

prepare() {
    # Clone git repository to get pkgver at smallest possible size
    if [[ ! -d osu ]]; then
     git clone --filter=tree:0 "${url}".git
    fi
    cd osu
    git fetch
}

pkgver() {
    # Get the latest osu-lazer version number
    cd osu
    git describe --tags $(git rev-list --tags --max-count=1)
}

build() {
    # Download the latest AppImage of osu-lazer and verify its checksum
    cd "${srcdir}"
    zsync https://github.com/ppy/osu/releases/download/"${pkgver}"/osu.AppImage.zsync
}

package() {
    install -Dm644 "${srcdir}"/"${_pkgname}".png "${pkgdir}"/usr/share/pixmaps/osu-lazer.png
    install -Dm755 "${srcdir}"/osu.AppImage "${pkgdir}"/usr/bin/osu-lazer
    install -Dm644 "${srcdir}"/"${_pkgname}".desktop "${pkgdir}"/usr/share/applications/"${_pkgname}".desktop
    install -Dm644 "${srcdir}"/LICENCE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENCE
}
